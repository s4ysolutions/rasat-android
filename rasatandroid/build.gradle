plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id "com.jfrog.bintray" version "1.8.5"
}

ext {
    releaseVersion = System.getenv('RELEASE_VERSION') ?: '1.0.3'
}

group 'solutions.s4y.rasat'
version releaseVersion


android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"


    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'solutions.s4y.rasat:rasat:1.0.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.res.srcDirs
    from android.sourceSets.main.manifest.srcFile
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task javadocJar (type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            RasatAndroidPublication(MavenPublication) {
                groupId = 'solutions.s4y.rasat'
                artifactId = 'rasat-android'
                version = releaseVersion

                from components.release
                artifact sourceJar
                artifact javadocJar

                pom {
                    packaging 'aar'
                    name = 'Rasat Android'
                    description = 'Android pub-sub java library above Rasat Java'
                    url = 'https://github.com/s4ysolutions/rasat-android'
                    licenses {
                        license {
                            name = 'MIT License (MIT)'
                            url = 'https://github.com/s4ysolutions/rasat-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'dsame'
                            name = 'Sergey Dolin'
                            email = 'sergey@s4y.solutions'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/s4ysolutions/rasat-android.git'
                        developerConnection = 'scm:git:ssh://github.com/s4ysolutions/rasat-android.git'
                        url = 'https://github.com/s4ysolutions/rasat-android'
                    }
                }
            }
        }
    }
}

bintray {
    dryRun = false
    publish = true
    override = true
    user = System.getenv('BINTRAY_USER') ?: bintrayUser
    key = System.getenv('BINTRAY_KEY') ?: bintrayKey
    publications = ['RasatAndroidPublication']

    pkg {
        repo = 'maven'
        name = 'rasat-android'
        userOrg = 's4ysolutions'
        description = 'Android wrapper above Rasat Java'
        publications = ['RasatAndroidPublication']
        licenses = ['MIT']
        vcsUrl = 'https://github.com/s4ysolutions/rasat-android.git'
        websiteUrl = 'https://github.com/s4ysolutions/rasat-android'
        issueTrackerUrl = 'https://github.com/s4ysolutions/rasat-android/issues'
        githubRepo = 's4ysolutions/rasat-android'
        githubReleaseNotesFile = 'README.md'
        version {
            name = releaseVersion
            desc = 'Rasat Android v' + releaseVersion
            released = new Date()
            vcsTag = releaseVersion
        }
    }
}

